// file:    alias.c
// date:    1992/09/24
// mudlib:  Basis
// purpose: storage and retrieval of aliases
// note:    based on code from Portals (changes made for reasons of efficiency)

#include <config.h>

private mapping alias;
private mapping xalias;

// clear_aliases: this needs called before the restore_object

void
clear_aliases()
{
	alias = ([]);
	xalias = ([]);
}

mapping
query_aliases()
{ 
	return alias + ([]); // return a copy
}

mapping
query_xaliases()
{ 
	return xalias + ([]);  // return a copy
}


string
query_alias(string str)
{
	string ret;

	if (undefinedp(ret = xalias[str])) {
		ret = alias[str];
	}
	return ret;
}

varargs int
add_alias(string verb, string cmd, int is_xalias)
{
	if (geteuid(previous_object()) != ROOT_UID) { // security
		return 0;
	}
	if (is_xalias) {
		xalias[verb[0..0]] = cmd;
	} else {
		alias[verb] = cmd;
	}
	return 1;
}

varargs int
remove_alias(string verb, int is_xalias)
{
	if (getuid(previous_object()) != ROOT_UID) { // security
		return 0;
	}
	if (is_xalias) {
		map_delete(xalias, verb); // okay if called when verb not in map
	} else {
		map_delete(alias, verb);
	}
	return 1;
}

// do_xalias: xaliases limited to 1 character in length for efficiency's
// sake.

string
do_xalias(string str)
{
	string value;

	if (value = xalias[str[0..0]]) {
		return replace_words(value, str[1 .. (strlen(str) - 1)]);
	}
	return 0;
}

string
do_alias(string str)
{
	string ret, first, rest;
  
	if (!str || (str == "")) {
		return "";
	}
	if (ret = do_xalias(str)) {
		return ret;
	}
	if (!sizeof(alias)) {
		return str;
	}
	if (sscanf(str, "%s %s", first, rest) != 2) {
		first = str;
		rest = "";
	}
	if (ret = alias[first]) {
		ret = replace_words(ret, rest);
		return ret;
	}
	return str;
}
